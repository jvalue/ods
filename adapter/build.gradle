plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'io.freefair.lombok' version '5.3.0'
}

def props = new Properties()
file("src/main/resources/application.properties").withInputStream { props.load(it)}

group = 'org.jvalue.ods'
version = props.getProperty("app.version")

java {
	sourceCompatibility = JavaVersion.VERSION_15
}

task testProps {
	doFirst {
		println version
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://jitpack.io' }
}

test {
  useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
		//showStandardStreams = true
		afterSuite { desc, result ->
			if (!desc.parent) {
				println "Tests run: ${result.testCount}, " +
						"Failures: ${result.failedTestCount}, " +
						"Skipped: ${result.skippedTestCount}"
			}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
	implementation 'org.postgresql:postgresql'
	implementation 'org.json:json:20210307'
	implementation 'com.github.everit-org.json-schema:org.everit.json.schema:1.12.3'
	implementation 'com.google.code.gson:gson:2.8.7'
	implementation 'com.vladmihalcea:hibernate-types-52:2.10.2'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

}
